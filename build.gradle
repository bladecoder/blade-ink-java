plugins {
    id "com.diffplug.spotless" version "8.1.0" apply false
    id("com.vanniktech.maven.publish") version "0.35.0"
}

if (!hasProperty("release") && !version.endsWith("-SNAPSHOT")) {
    version += "-SNAPSHOT"
}

// Display version
println "Building version: $version"

// Configuración común para todos los subproyectos
subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.vanniktech.maven.publish'

    group = 'com.bladecoder.ink'

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'junit:junit:4.13'
    }

    // DISABLES JAVADOC ULTRACHECKS IN JDK8
    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    spotless {
        java {
            target fileTree('src') {
                include '**/*.java'
            }
            toggleOffOn()
            palantirJavaFormat()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    jar {
        manifest {
            attributes(
                'github'   : 'https://github.com/bladecoder/blade-ink/',
                'license'  : 'Apache-2.0',
                'group'    : project.group,
                'version'  : project.version,
                'java'     : project.java.targetCompatibility,
                'timestamp': System.currentTimeMillis()
            )
        }
    }

    javadoc {
        options {
            memberLevel = JavadocMemberLevel.PUBLIC
            author true
            setUse true
            encoding "UTF-8"
        }
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : "$System.env.NEXUS_USERNAME"
                    password project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : "$System.env.NEXUS_PASSWORD"
                }
            }
        }
    }
}
