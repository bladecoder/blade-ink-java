apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'com.bladecoder.ink'

sourceCompatibility = 1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

jar {
	manifest.attributes += [
			'github': 'https://github.com/bladecoder/blade-ink/',
			'license': 'Apache-2.0',
			'group': project.group,
			'version': project.version,
			'java': targetCompatibility,
			'timestamp': System.currentTimeMillis()
	]
}

javadoc {
	title = "Blade Ink"
	options {
		memberLevel = JavadocMemberLevel.PUBLIC
		author true
		setUse true
    		encoding "UTF-8"
	}
}


task enginedocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives enginedocJar
    archives sourcesJar
}

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

//set build variables based on build type (release, continuous integration, development)
if(hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
}

//********* artifact signing *********
if(isReleaseBuild) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        }
        else {
            mavenDeployer {
                if(isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                  authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                   name 'blade-ink'
                   packaging 'jar'
                   description "This is a Java port of inkle's ink, a scripting language for writing interactive narrative."
                   url 'https://github.com/bladecoder/blade-ink'

                   scm {
                       url 'scm:git@github.com:bladecoder/blade-ink.git'
                       connection 'scm:git@github.com:bladecoder/blade-ink.git'
                       developerConnection 'scm:git@github.com:bladecoder/blade-ink.git'
                   }

                   licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                   }

                   developers {
                       developer {
                           id 'rgarcia'
                           name 'Rafael Garcia'
                       } 
                   }
               }
            }
        }
    }
}


