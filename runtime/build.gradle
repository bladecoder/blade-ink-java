plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.diffplug.spotless" version "7.2.1"
}

group = 'com.bladecoder.ink'

sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
}

if (!hasProperty("release") && !version.endsWith("-SNAPSHOT")) {
    version += "-SNAPSHOT"
}

// DISABLES JAVADOC ULTRACHECKS IN JDK8
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

spotless {
    java {
        target fileTree('src') {
            include '**/*.java'
        }
        toggleOffOn()
        palantirJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

jar {
    manifest.attributes += [
            'github'   : 'https://github.com/bladecoder/blade-ink/',
            'license'  : 'Apache-2.0',
            'group'    : project.group,
            'version'  : project.version,
            'java'     : targetCompatibility,
            'timestamp': System.currentTimeMillis()
    ]
}

javadoc {
    title = "Blade Ink"
    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        author true
        setUse true
        encoding "UTF-8"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'blade-ink'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'blade-ink'
                description = "This is a Java port of inkle's ink, a scripting language for writing interactive narrative."
                url = 'https://github.com/bladecoder/blade-ink'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'bladecoder'
                        name = 'Rafael Garcia'
                        email = 'bladecoder@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:bladecoder/blade-ink.git'
                    developerConnection = 'scm:git@github.com:bladecoder/blade-ink.git'
                    url = 'scm:git@github.com:bladecoder/blade-ink.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : "$System.env.NEXUS_USERNAME"
                password project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}

signing {
    if (!version.endsWith('SNAPSHOT')) {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


